# Original Author: Sairon Istyar <saironiq@gmail.com>
# Contributor: Jon <jonathan.stott@gmail.com> (See https://gist.github.com/namelessjon/9651948)
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-annex-standalone
pkgver=5.20140306
pkgrel=4
pkgdesc='Standalone precompiled version of git-annex bundled with libs and utils, batteries included.'
arch=('i686' 'x86_64')
url='http://git-annex.branchable.com/'
license=('GPL3')
provides=('git-annex')
conflicts=('git-annex' 'git-annex-bin')
source=('git-annex' 'runshell.patch')
sha256sums=('c7d12ba3f3a00736d0d22e18e76721314d461561adcf48cbd102413e5e75be92'
            '173b3f0dc5a314559574113a2cc1242b61ff7caebbd3be52d3090b7a91e1268f')

# binaries to remove from resulting standalone package
_rmbin=('cp' 'curl' 'git' 'gpg' 'lsof' 'rsync' 'sh' 'sha1sum' 'sha224sum' \
        'sha256sum' 'sha384sum' 'sha512sum' 'ssh' 'wget' 'xargs' \
        'ssh-keygen' 'tar' 'git-shell' 'git-upload-pack' 'git-receive-pack' 'gunzip' )

# all of these can be left out if not deleting $_rmbin binaries
depends=('coreutils' 'curl' 'git' 'gnupg' 'lsof' 'rsync' 'bash' 'openssh' 'wget' 'findutils' 'dnsutils')

if [[ $CARCH == "x86_64" ]] ; then
    _file=git-annex-standalone-amd64-${pkgver}.tar.gz
    source+=("${_file}::https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz")
    sha256sums+=('113c1f9edd728466c91872b3026e7b76746ab8a9ebef7127ba9b95d9f10b24e6')
else
    _file=git-annex-standalone-i386-${pkgver}.tar.gz
    source+=("${_file}::https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-i386.tar.gz")
    sha256sums+=('5838b7beda80751d6ad11c465e5c166dcd23f4b6a51c239502146f9112372919')
fi

package() {
    mkdir -p "$pkgdir/opt" "$pkgdir/usr/bin"

    cp -a "$srcdir/git-annex.linux" "$pkgdir/opt/"

    patch "$pkgdir/opt/git-annex.linux/runshell" "$srcdir/runshell.patch"

    for file in ${_rmbin[@]} ; do
        msg "  Removing $file from executables..."
        rm "$pkgdir/opt/git-annex.linux/bin/$file" || true # curl and lsof do not exist in 64-bit build"
        #msg "  Removing $file from shimmed excutables..."
        #rm -r "$pkgdir/opt/git-annex.linux/shimmed/$file" || true # curl and lsof do not exist in 64-bit build"
    done

    ## clean up git from the shimmed directory too
    #for file in $(ls "$pkgdir/opt/git-annex.linux/git-core/")
    #do
        #ls -al "$pkgdir/opt/git-annex.linux/shimmed/$file"
        #rm -r "$pkgdir/opt/git-annex.linux/shimmed/$file" || true # ignore missing files
    #done

    # Remove runshell git wrapper that causes recursive loops for some systems
    rm "$pkgdir/opt/git-annex.linux/git"
    rm "$pkgdir/opt/git-annex.linux/git-receive-pack"
    rm "$pkgdir/opt/git-annex.linux/git-upload-pack"
    rm "$pkgdir/opt/git-annex.linux/git-shell"
    rm -r "$pkgdir/opt/git-annex.linux/git-core"

    # 64-bit curl fix (endless loop in assistant when creating new repo - incompatible curl bin (system) and lib (package))
    if [[ $CARCH == "x86_64" ]] ; then
        rm "$pkgdir/opt/git-annex.linux/usr/lib/x86_64-linux-gnu/libcurl.so.4"
    fi

    # move the man pages over to the system directory
    mv "$pkgdir/opt/git-annex.linux/usr/share" "$pkgdir/usr/"
    gzip -9 "$pkgdir/usr/share/man/man1/git-annex.1"
    gzip -9 "$pkgdir/usr/share/man/man1/git-annex-shell.1"

    cp "$srcdir/git-annex" "$pkgdir/usr/bin/"
    chmod +x "$pkgdir/usr/bin/git-annex"
    ln -s git-annex "$pkgdir/usr/bin/git-annex-webapp"
    ln -s git-annex "$pkgdir/usr/bin/git-annex-shell"
}
